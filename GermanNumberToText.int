zahl(num="",gen="") Public {
 ;;; convert number as German text
 ;;; w $$^zahl(-1123.505) >>>> Minus ein Tausend einhundertdreiundzwanzig Komma fünf null fünf

 set dec=$p(num,".",2),dec=$s(dec?1.N:$$dec(dec),1:"")
 if num=0 quit "null"_dec
 set neg=$S(num<0:"Minus ",1:"")
 if $l(neg) set num=$tr(num,"-")
 if num=1 set gen=$zcvt(gen,"U") quit neg_"ein"_$case(gen,"W":"e","S":"es","M":"",:"s")
 if num<10e23 quit neg_$$trd($p(num,"."))_dec
 quit "*** Zahl zu groß ***"
}
 ;
dec(num) {
 set dec=" Komma"
 for p=1:1:$l(num) set dec=dec_" "_$$zig($e(num,p)) 
 quit dec
}
zig(num) {
 if num<10 quit $li($lb("null","eins","zwei","drei","vier","fünf","sechs","sieben","acht","neun"),num+1)
 if num<20 quit $li($lb("zehn","elf","zwölf","dreizehn","vierzehn","fünfzehn","sechzehn","siebzehn","achtzehn","neunzehn"),num-9)
 set zig=$e(num,*-1),zn=$e(num,*)
 set res=$s(zig=3:"dreißig"
           ,1:$li($lb(,"zwan",,"vier","fünf","sech","sieb","acht","neun"),zig)_"zig")
 if zn set res=$s(zn=1:"ein",1:$$zig(zn))_"und"_res
 quit res 
}
hun(num) {
 set hun=$e(num,*-2),zig=$e(num,*-1,*),res="",m="hundert"
 set res=$s(hun=1:"ein"_m
           ,hun>1:$$zig(hun)_m
           ,1:"" )
 quit $replace(res_$$zig(zig),"null","")
}
ein(res) {
 if $e(res,*-3,*)="eins" set res=$e(res,1,*-1)
 quit $replace(res,"null","")
} 
tsd(num) { ;1,000  10e3
 set tsd=$e(num,*-5,*-3),hun=$e(num,*-2,*),res=""
 if tsd set res=$$ein($$hun(tsd))_" Tausend "
 quit res_$$hun(hun)
}
mio(num) { ;1,000,000  10e6
 set mio=$e(num,*-8,*-6),tsd=$e(num,*-5,*),m=" Million"
 set res=$s(mio=1:"eine"_m_" "
           ,mio>1:$$ein($$hun(mio))_m_"en "
           ,1:"")  
 quit res_$$tsd(tsd)
}
mrd(num) { ;1,000,000,000  10e9
 set mrd=$e(num,*-11,*-9),mio=$e(num,*-8,*),m=" Milliarde"
 set res=$s(mrd=1:"eine"_m_" "
           ,mrd>1:$$ein($$hun(mrd))_m_"n "
           ,1:"" )
 quit res_$$mio(mio)
}
bio(num) { ;1,000,000,000,000 10e12
 set bio=$e(num,*-14,*-12),mrd=$e(num,*-11,*),m=" Billion"
 set res=$s(bio=1:"eine"_m_" "
           ,bio>1:$$ein($$hun(bio))_m_"en "
           ,1:"" )
 quit res_$$mrd(mrd)
}
brd(num) { ;1,000,000,000,000,000 10e15
 set brd=$e(num,*-17,*-15),bio=$e(num,*-14,*),res="",m=" Billiarde"
 set res=$s(brd=1:"eine"_m_" "
           ,brd>1:$$ein($$hun(brd))_m_"n"_" "
           ,1:"" )
 quit res_$$bio(bio)
}
tri(num) {;1,000,000,000,000,000,000 10e18
 set tri=$e(num,*-20,*-18),brd=$e(num,*-17,*),m=" Trillion"
 set res=$s(tri=1:"eine"_m_" "
           ,tri>1:$$ein($$hun(tri))_m_"en"_" "
           ,1:"" )
 quit res_$$brd(brd)
}   
trd(num) { ;1,000,000,000,000,000,000,000 10e21 
 set trd=$e(num,*-23,*-21),tri=$e(num,*-20,*),m=" Trilliarde"
 set res=$s(trd=1:"eine"_m_" "
           ,trd>1:$$ein($$hun(trd))_m_"n"_" "
           ,1:"" )
 quit res_$$tri(tri)
}
